import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'idea'

    // For building a fat jar
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // For automatic syntax formatting
    id "com.diffplug.gradle.spotless" version "4.3.0"
}

group 'dev.yerbie'
version '1.1.0'

sourceCompatibility = 1.9

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '2.0.10'
    compile group: 'redis.clients', name: 'jedis', version: '3.3.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

run {
    args 'server', './yerbie.yml'
}

// Merges entries in META-INF/services into a single resource.
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

spotless {
    java {
        googleJavaFormat()
    }
}

test {
    useJUnit()
}

processResources {
    filter ReplaceTokens, tokens: [
            "yerbie_version": project.property('version')
    ]
}
mainClassName = 'dev.yerbie.YerbieApplication'
